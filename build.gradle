apply plugin: 'java'
apply plugin: 'checkstyle'

jar.baseName = 'atm-yellow-box'
version = '1.0'

repositories {
  jcenter()
}

dependencies {
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
  compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.8.5'
  compile 'com.h2database:h2:1.4.193'

}

checkstyle {
  configFile = rootProject.file('config/checkstyle/checkstyle.xml')
  checkstyleTest.enabled = false
  toolVersion = '7.1.2'
}

compileJava {
  options.compilerArgs << '-Xlint:unchecked'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

jar {
  manifest {
    attributes 'Implementation-Title': 'ATM yellow box',
            'Implementation-Version': version,
            'Created-By': 'aa-team',
            'Main-Class': 'Main'
  }
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
  manifest {
    attributes 'Implementation-Title': 'ATM yellow box',
            'Implementation-Version': version,
            'Created-By': 'aa-team',
            'Main-Class': 'Main'
  }
    baseName = 'atm-yellow-box' + '-full'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task execute(type:JavaExec) {
  if (project.hasProperty('mainClass')) {
    main = mainClass
  } else {
    main = 'Main'
  }
  standardInput = System.in
  classpath = sourceSets.main.runtimeClasspath
}
